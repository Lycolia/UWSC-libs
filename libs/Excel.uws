Call Common.uws
Call RegEx.uws
Call Ctrl.uws

// エクセル操作
MODULE Excel

//#region シート操作

    // シートの挿入
    PROCEDURE insertSheet(name = "")
        KBD(VK_ESC, DOWN, 0)
        // add sheet
        KBD(VK_ALT, DOWN, 0)
        KBD(VK_SHIFT, DOWN, 0)
        KBD(VK_F1, CLICK, 100)
        KBD(VK_SHIFT, UP, 0)
        // move end
        KBD(VK_E, CLICK, 100)
        KBD(VK_M, CLICK, 100)
        KBD(VK_END, CLICK, 100)
        KBD(VK_ALT, UP, 100)
        KBD(VK_RETURN, CLICK, 100)
        IF name <> "" THEN
            this.renameSheet(name)
        ENDIF
    FEND

    // アクティブシートのリネーム
    PROCEDURE renameSheet(name)
        KBD(VK_ESC, DOWN, 0)
        // change input
        KBD(VK_ALT, DOWN, 100)
        KBD(VK_H, CLICK, 100)
        KBD(VK_O, CLICK, 100)
        KBD(VK_R, CLICK, 100)
        KBD(VK_ALT, UP, 100)
        // clear input
        KBD(VK_DELETE, CLICK, 100)
        // send input
        SLEEP(0.2)
        SENDSTR(GETID(GET_ACTIVE_WIN), name)
        // fix
        KBD(VK_RETURN, CLICK, 100)
    FEND

//#endregion

//#region セル移動

    // セルジャンプ
    PROCEDURE jump(position)
        KBD(VK_ESC, DOWN, 0)
        Ctrl.g()
        SENDSTR(GETID(GET_ACTIVE_WIN), position)
        SLEEP(0.2)
        KBD(VK_RETURN, CLICK, 100)
    FEND

    // 1セル上に
    PROCEDURE selectUp()
        KBD(VK_UP, CLICK, 100)
    FEND

    // 1セル下に
    PROCEDURE selectDown()
        KBD(VK_DOWN, CLICK, 100)
    FEND
    
    // 1セル左に
    PROCEDURE selectLeft()
        KBD(VK_LEFT, CLICK, 100)
    FEND
    
    // 1セル右に
    PROCEDURE selectRight()
        KBD(VK_RIGHT, CLICK, 100)
    FEND

//#endregion

//#region 選択セル座標

    // 現在選択されているセルの座標情報を取得、エクセルがフルウィンドウでリボン出てる必要あり
    FUNCTION getSelectedCellPosition()
        BTN(LEFT, CLICK, 45, 170, 100)
        Ctrl.c()
        RESULT = GETSTR(0)
    FEND

    // 現在選択されているセルの行番号を取得、エクセルがフルウィンドウでリボン出てる必要あり、A1形式のみ対応
    FUNCTION getSelectedRow()
        this.getSelectedCellPosition()
        RESULT = RegEx.replace("^[A-Z]+?([0-9]+)$", GETSTR(0), "$1")
    FEND

    // 現在選択されているセルの列号を取得、エクセルがフルウィンドウでリボン出てる必要あり、A1形式のみ対応
    FUNCTION getSelectedColumn()
        this.getSelectedCellPosition()
        RESULT = RegEx.replace("^([A-Z]+?)[0-9]+$", GETSTR(0), "$1")
    FEND

//#endregion

//#region セル入力

    // 選択セルにタブ区切りで値を一括書き込み
    PROCEDURE writeCell(writeItems[])
        Dim buf = ""
        FOR item IN writeItems
            buf = buf + item + "<#TAB>"
        NEXT
        
        buf = RegEx.replace("\t*$", buf, "")
        SENDSTR(0, buf)
        Ctrl.v()
    FEND

//#endregion

ENDMODULE