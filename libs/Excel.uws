Call Common.uws
Call RegEx.uws

// エクセル操作
MODULE Excel

//#region シート操作

    // シートの挿入
    PROCEDURE insertSheet(name = "")
        KBD(VK_ESC, CLICK, 0)
        SLEEP(0.2)
        // シート追加
        SCKEY(GETID(GET_ACTIVE_WIN), VK_ALT, VK_SHIFT, VK_F1)
        SLEEP(0.5)
        // シート移動
        SCKEY(GETID(GET_ACTIVE_WIN), VK_ALT, VK_E, VK_M)
        SLEEP(0.5)
        KBD(VK_END, CLICK, Common.KBD_WAIT)
        KBD(VK_RETURN, CLICK, Common.KBD_WAIT)
        IF name <> "" THEN
            SLEEP(0.2)
            this.renameSheet(name)
        ENDIF
    FEND

    // アクティブシートのリネーム
    PROCEDURE renameSheet(name)
        KBD(VK_ESC, DOWN, 0)
        // シート名編集状態に変更
        SCKEY(GETID(GET_ACTIVE_WIN), VK_ALT, VK_H, VK_O, VK_R)
        SLEEP(0.2)
        // 全削除
        KBD(VK_DELETE, CLICK, Common.KBD_WAIT)
        SLEEP(0.2)
        // テキスト入力
        SENDSTR(GETID(GET_ACTIVE_WIN), name)
        KBD(VK_RETURN, CLICK, Common.KBD_WAIT)
        SLEEP(0.2)
    FEND

//#endregion

//#region セル移動

    // セルジャンプ
    PROCEDURE jump(position)
        KBD(VK_ESC, CLICK, 0)
        SCKEY(GETID(GET_ACTIVE_WIN), VK_CTRL, VK_G)
        SLEEP(0.2)
        SENDSTR(GETID(GET_ACTIVE_WIN), position)
        SLEEP(0.2)
        KBD(VK_RETURN, CLICK, Common.KBD_WAIT)
    FEND

    // 1セル上に
    PROCEDURE selectUp()
        KBD(VK_UP, CLICK, Common.KBD_WAIT)
    FEND

    // 1セル下に
    PROCEDURE selectDown()
        KBD(VK_DOWN, CLICK, Common.KBD_WAIT)
    FEND
    
    // 1セル左に
    PROCEDURE selectLeft()
        KBD(VK_LEFT, CLICK, Common.KBD_WAIT)
    FEND
    
    // 1セル右に
    PROCEDURE selectRight()
        KBD(VK_RIGHT, CLICK, Common.KBD_WAIT)
    FEND

//#endregion

//#region 選択セル座標

    // 現在選択されているセルの座標情報を取得、エクセルがフルウィンドウでリボン出てる必要あり
    FUNCTION getSelectedCellPosition()
        BTN(LEFT, CLICK, 45, 170, 100)
        SLEEP(0.1)
        SCKEY(GETID(GET_ACTIVE_WIN), VK_CTRL, VK_C)
        SLEEP(0.2)
        RESULT = GETSTR(0)
    FEND

    // 現在選択されているセルの行番号を取得、エクセルがフルウィンドウでリボン出てる必要あり、A1形式のみ対応
    FUNCTION getSelectedRow()
        RESULT = RegEx.replace("^[A-Z]+?([0-9]+)$", "$1", this.getSelectedCellPosition())
    FEND

    // 現在選択されているセルの列号を取得、エクセルがフルウィンドウでリボン出てる必要あり、A1形式のみ対応
    FUNCTION getSelectedColumn()
        RESULT = RegEx.replace("^([A-Z]+?)[0-9]+$", "$1", this.getSelectedCellPosition())
    FEND

//#endregion

//#region セル入力

    // 選択セルにタブ区切りで値を一括書き込み
    PROCEDURE writeCell(writeItems[])
        Dim buf = ""
        FOR item IN writeItems
            buf = buf + item + "<#TAB>"
        NEXT
        
        buf = RegEx.replace("\t*$", "", buf)
        SENDSTR(0, buf)
        SCKEY(GETID(GET_ACTIVE_WIN), VK_CTRL, VK_V)
    FEND

//#endregion

ENDMODULE